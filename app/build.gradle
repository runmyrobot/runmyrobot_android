apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

/**
 * Get a specified secret from local.properties. Needed to get letsrobot info
 * TODO make configurable through app
 * @param secretID
 * @return String
 */
String getProp(String secretID){
    //Load secrets from the local.properties file
    File secretPropsFile = file('../local.properties')
    if (secretPropsFile.exists()) {
        Properties p = new Properties()
        p.load(new FileInputStream(secretPropsFile))
        p.getProperty(secretID, "\"\"")
    } else {
        "\"\""
    }

}

android {
    compileSdkVersion 27

    defaultConfig {
        applicationId "com.runmyrobot.android_robot_for_phone"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        buildConfigField "String", "ROBOT_ID", getProp("robot.robotID")
        buildConfigField "String", "ROBOT_PASS", getProp("robot.robotPass")
        buildConfigField "String", "CAMERA_ID", getProp("robot.cameraID")
        buildConfigField "String", "ERROR_REPORTING", getProp("app.reportErrors")
        String cameraPass = getProp("robot.cameraPass")
        if(cameraPass == "\"\"") cameraPass = "\"hello\""
        buildConfigField "String", "CAMERA_PASS", cameraPass

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation('io.socket:socket.io-client:1.0.0') {
        // excluding org.json which is provided by Android
        exclude group: 'org.json', module: 'json'
    }
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    testImplementation 'junit:junit:4.12'
    implementation 'com.android.support:appcompat-v7:27.1.1'
    // Required for instrumented tests
    androidTestImplementation 'com.android.support:support-annotations:27.1.1'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    implementation 'com.github.felHR85:UsbSerial:4.5.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.squareup.okhttp3:okhttp:3.11.0'
    //implementation 'com.writingminds:FFmpegAndroid:0.3.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    api 'com.google.guava:guava:26.0-android'
    implementation project(':FFmpegAndroid-release')
    //implementation project(':controller-api')
    //implementation project(':logging')
    implementation 'com.akexorcist:bluetoothspp:1.0.0'
    implementation 'com.bugsnag:bugsnag-android:4.+'
}
repositories {
    mavenCentral()
}
